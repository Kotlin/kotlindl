/*
 * Copyright 2020 JetBrains s.r.o. and Kotlin Deep Learning project contributors. All Rights Reserved.
 * Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE.txt file.
 */

apply plugin: 'maven-publish'
apply from: project.rootProject.file('gradle/maven-metadata.gradle')

def isMultiplatform = project.name in ["dataset", "api", "onnx", "visualization"]

task javadocJar(type: Jar) {
    classifier 'javadoc'
}

def sonatypeUser = System.getenv('SONATYPE_USER')
def sonatypePassword = System.getenv('SONATYPE_PASSWORD')

def moduleName = "kotlin-deeplearning-" + project.name

nexusStaging {
    username sonatypeUser
    password sonatypePassword
    packageGroup project.group.toString()
    repositoryDescription "kotlinx.kotlindl staging repository, version: $version"
    delayBetweenRetriesInMillis 5000
}

afterEvaluate {
    publishing {
        if (!isMultiplatform) {
            publications {
                maven(MavenPublication) { publication ->
                    pom.withXml(configureMavenCentralMetadata)
                    from components.java
                    artifactId moduleName
                    groupId project.group
                    version project.version
                    artifact javadocJar
                    artifact sourcesJar
                }
            }
            return
        }
        publications.configureEach {
            def type = it.name
            switch (type) {
                case 'kotlinMultiplatform':
                    it.artifactId "$moduleName"
                    // in order to be compatible with the clients which can't read module metadata and get the correct variant
                    // this code changes the pom file so that these clients get the jvm variant
                    // same as done in the kotlinx.serialization library
                    // see https://github.com/Kotlin/kotlinx.serialization/blob/16a85df254f4f1e317554eb61ee1fbe914800aa4/gradle/publishing.gradle#L70
                    apply from: "$rootDir/gradle/publish-mpp-root-module-in-platform.gradle"
                    publishPlatformArtifactsInRootModule(publications.jvm)
                    break
                case 'androidRelease':
                    def artifact = it
                    // afterEvaluate here is needed to workaround https://youtrack.jetbrains.com/issue/KT-53520
                    afterEvaluate { artifact.artifactId "$moduleName-android" }
                    break
                default:
                    it.artifactId "$moduleName-$type"
                    break
            }
            groupId project.group
            version project.version
            pom.withXml(configureMavenCentralMetadata)
            if (name != "kotlinMultiplatform") {
                artifact javadocJar
            }
        }
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = sonatypeUser
                    password = sonatypePassword
                }
            }
        }
    }
}

def signingKey = System.getenv("SIGN_KEY_ID")
def signingKeyPassphrase = System.getenv("SIGN_KEY_PASSPHRASE")
def signingPrivateKey = System.getenv("SIGN_KEY_PRIVATE")

if (signingKey != null) {
    apply plugin: 'signing'
    signing {
        useInMemoryPgpKeys(signingKey, signingPrivateKey, signingKeyPassphrase)
        sign publishing.publications
    }
}
