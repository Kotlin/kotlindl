plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.7.10' apply false
    id 'com.android.application' apply false
    id 'maven-publish'
    id 'io.codearte.nexus-staging' version '0.22.0'
    id 'org.jetbrains.dokka' version '1.5.30'
    id "com.gradle.plugin-publish" version "1.0.0" apply false
}

apply from: project.rootProject.file('gradle/dokka.gradle')

def kotlinDLVersion = "$majorVersion.$minorVersion"

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    group = 'org.jetbrains.kotlinx'
    version = kotlinDLVersion
}

def unpublishedSubprojects = ["examples", "gradlePlugin"]

subprojects {
    if (name in unpublishedSubprojects) return
    apply from: project.rootProject.file('gradle/publish.gradle')
}

task setTeamcityVersion {
    doLast {
        println("##teamcity[buildNumber '$version']")
    }
}

apply from: project.rootProject.file('gradle/fatJar.gradle')