plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'maven-publish'
    id 'io.codearte.nexus-staging' version '0.22.0'
    id 'org.jetbrains.dokka' version '1.5.30'
}

apply plugin: "io.codearte.nexus-staging"
apply from: project.rootProject.file('gradle/dokka.gradle')

def kotlinDLVersion = "$majorVersion.$minorVersion"

allprojects {
    group = 'org.jetbrains.kotlinx'
    version = kotlinDLVersion

    apply plugin: "maven-publish"
    apply plugin: "kotlin"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
}

subprojects {
    repositories {
        mavenCentral()
    }
}

def unpublishedSubprojects = ["examples"]

subprojects {
    if (name in unpublishedSubprojects) return
    apply from: project.rootProject.file('gradle/publish.gradle')
}

task setTeamcityVersion {
    doLast {
        println("##teamcity[buildNumber '$version']")
    }
}